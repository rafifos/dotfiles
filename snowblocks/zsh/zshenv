# /etc/zsh/zshenv: system-wide .zshenv file for zsh(1).
#
# This file is sourced on all invocations of the shell.
# If the -f flag is present or if the NO_RCS option is
# set within this file, all other initialization files
# are skipped.
#
# This file should contain commands to set the command
# search path, plus other important environment variables.
# This file should not contain commands that produce
# output or assume the shell is attached to a tty.
#
# Global Order: zshenv, zprofile, zshrc, zlogin

# Set's "Portuguese (Brazil)" as the fallback language.
export LC_ALL=${LC_ALL:-pt_BR.UTF-8}

# Sets nvim as the default editor if it's installed.
if type nvim >/dev/null; then
  export EDITOR="nvim"
  export MANPAGER="nvim -R +MANPAGER -"
  export VISUAL="nvim"
fi

# Makes Linuxbrew available to the shell.
test -d "${HOME}/.linuxbrew" && eval "$(${HOME}/.linuxbrew/bin/brew shellenv)"
test -d '/home/linuxbrew/.linuxbrew' && eval "$(/home/linuxbrew/.linuxbrew/bin/brew shellenv)"

# I'm using zinit because of it's performance-related features (like turbo (someone give a trophel to the person who
# thought this was a good name) mode and automatic compilation of modules).
typeset -A ZINIT
ZINIT[ZCOMPDUMP_PATH]="${XDG_CACHE_HOME:-${HOME}/.cache}/zinit"
ZINIT[HOME_DIR]="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit"
ZINIT[BIN_DIR]="${ZINIT[HOME_DIR]}/bin"
ZINIT[PLUGINS_DIR]="${ZINIT[HOME_DIR]}/plugins"
ZINIT[COMPLETIONS_DIR]="${ZINIT[HOME_DIR]}/completions"
ZINIT[SNIPPETS_DIR]="${ZINIT[HOME_DIR]}/snippets"

export ZINIT
export ZPFX="${ZINIT[HOME_DIR]}/polaris"

if [[ ! -f "${ZINIT[BIN_DIR]}/zinit.zsh" ]]; then
  print -P "%F{33}▓▒░ %F{220}Installing %F{33}DHARMA%F{220} Initiative Plugin Manager (%F{33}zdharma/zinit%F{220})…%f"
  command mkdir -p "${ZINIT[HOME_DIR]}" && command chmod g-rwX "${ZINIT[HOME_DIR]}"
  command git clone https://github.com/zdharma/zinit "${ZINIT[BIN_DIR]}" \
    && print -P "%F{33}▓▒░ %F{34}Installation successful.%f%b" \
    || print -P "%F{160}▓▒░ The clone has failed.%f%b"

  # See: https://github.com/zdharma/zinit#zinit-module
  source "${ZINIT[BIN_DIR]}/zinit.zsh" && zinit module build || return 1
fi

# Skips the globally defined `compinit` execution, this is sometimes set by distro's maintainers but doesn't really help
# the users who sets it by themselves.
skip_global_compinit=1

# Makes zinit available to the shell.
source "${ZINIT[BIN_DIR]}/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# Builds the
module_path+=( "${ZINIT[BIN_DIR]}/zmodules/Src" )
zmodload zdharma/zplugin

# Adds functions to $fpath.
if [[ $PMSPEC != *f* ]] {
  fpath+=( "${ZDOTDIR}/functions" )
}

# Autoload required functions.
autoload -Uz ${ZDOTDIR}/functions/*(.)

# Local plugins.
zinit load _local/docker
zinit load _local/gpg

# Zsh plugin for installing, updating and loading nvm.
zinit atinit='_zsh_nvm_atinit' for lukechilds/zsh-nvm
